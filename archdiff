#!/usr/bin/perl

use strict;
use warnings;
use English qw( -no_match_vars );
use Getopt::Long;
use File::Temp qw/ tempdir /;
use File::Spec;
use Cwd;

our $VERSION = '0.1';

my @DEFAULT_DIFFARGS = qw/-r --brief/;
my $CWD              = getcwd();

my $QUIET   = 0;
my $VERBOSE = 0;

run_archdiff(@ARGV) unless defined caller;

sub run_archdiff {
    my (@argv) = @_;

    my %opts;

    Getopt::Long::Configure("no_auto_abbrev");
    GetOptionsFromArray( \@argv, \%opts, 'verbose|v', 'quiet|q', 'help|h|?',
        'no-autostrip|n', 'version' );

    $VERBOSE = $opts{verbose};
    $QUIET   = $opts{quiet};

    if ( $opts{'help'} ) {
        show_usage();
        exit;
    }

    if ( $opts{'version'} ) {
        show_version();
        exit;
    }

    my ( $left_file, $right_file, @diffargs ) = @argv;
    if ( scalar @argv < 2 ) {
        ERROR("You must pass in two archives to compare.");
        exit 1;
    }

    @diffargs = @DEFAULT_DIFFARGS unless @diffargs;

    DEBUG("left side: $left_file");
    DEBUG("right side: $right_file");
    DEBUG("diff args: @diffargs");

    my $left_dir  = file_to_dir( $left_file,  \%opts );
    my $right_dir = file_to_dir( $right_file, \%opts );

    system( 'diff', @diffargs, $left_dir, $right_dir );

    # propagate diff's exit code out
    exit $? >> 8;
}

sub file_to_dir {
    my ( $filename, $options ) = @_;

    if ( -d $filename ) {
        return $filename;
    }

    if ( !-e $filename ) {
        die "Unable to find file '$filename'\n";
    }

    DEBUG("original filename: $filename");
    my ( $toss_vol, $toss_path, $bare_filename )
        = File::Spec->splitpath($filename);
    DEBUG("bare filename: $filename");

    my $dir = tempdir( $bare_filename . '-XXXX', DIR => $CWD, CLEANUP => 1 );

    my @unpack_command;
    if ( $filename =~ m{.tar.gz} || $filename =~ m{.tgz} ) {
        @unpack_command = ( 'tar', '-C', $dir, '-xzf', $filename );
    }
    elsif ( $filename =~ m{.tar.bz} || $filename =~ m{.tbz} ) {
        @unpack_command = ( 'tar', '-C', $dir, '-xjf', $filename );
    }
    elsif ( $filename =~ m{.tar} ) {
        @unpack_command = ( 'tar', '-C', $dir, '-xf', $filename );
    }
    elsif ( $filename =~ m{.zip} ) {
        @unpack_command = ( 'unzip', '-d', $dir, $filename );
    }

    DEBUG( "unpacking $filename: " . join( ' ', @unpack_command ) );
    system(@unpack_command);

    if ( !$options->{'no-autostrip'} ) {
        opendir( my $dirhandle, $dir ) || die "can't open $dir: $!";
        my @entries = grep { $_ ne '.' && $_ ne '..' } readdir($dirhandle);
        closedir($dirhandle);

        if ( scalar @entries == 1 && -d "$dir/$entries[0]" ) {
            $dir = "$dir/$entries[0]";
        }
    }
    return File::Spec->abs2rel( $dir, $CWD );
}

# a few log4perl-alikes
sub ERROR {
    printf "ERROR: %s\n", shift;
}

sub WARN {
    printf "WARN: %s\n", shift;
}

sub INFO {
    printf "INFO: %s\n", shift if !$QUIET;
}

sub DEBUG {
    printf "DEBUG: %s\n", shift if $VERBOSE;
}

sub show_usage {
    show_version();
    print <<END;

Usage:
    archdiff one.tar.gz two.tar.gz
    archdiff one.tgz two.zip
    archdiff one.zip two/

For full documentation, run "perldoc $0".
END
}

sub show_version {
    print "archdiff version $VERSION\n";
}

# work-alike for function from perl 5.8.9 and later
# added for compatibility with CentOS 5, which is stuck on 5.8.8
sub GetOptionsFromArray {
    my ( $argv, $opts, @options ) = @_;

    local @ARGV = @$argv;
    GetOptions( $opts, @options );

    # update the passed argv array
    @$argv = @ARGV;
}

1;

__END__

=head1 NAME

    archdiff - Show high level differences between archives.

=head1 SYNOPSIS

    archdiff one.tar.gz two.tar.gz
    archdiff one.tgz two.zip
    archdiff one.zip two/

=head1 DESCRIPTION

Takes two archives, or an archive and a directory and shows a summary of the
differences between them.

=head1 OPTIONS

  --no-autostrip -n     Normally, if an archive has only one directory, that
                        directory is compared instead of the root of the
                        archive.  This option disables that behavior.
  --verbose             Show extra information about what archdiff is doing
  --quiet               Show as little info as possible.
  --version             Print version information.
  --help                Show help.

=head1 AUTHOR

Nate Jones <nate@endot.org>

=head1 COPYRIGHT

Copyright (c) 2012 L</AUTHOR> as listed above.

=head1 LICENSE

This program is free software distributed under the Artistic License 2.0.

=cut
